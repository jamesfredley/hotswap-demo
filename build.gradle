buildscript {
    repositories {
        mavenCentral()
        maven {
           url = 'https://repo.grails.org/grails/restricted'
        }
        maven {
           url = 'https://repository.apache.org/content/groups/snapshots'
           content {
              includeVersionByRegex('org[.]apache[.](grails|groovy).*', '.*', '.*-SNAPSHOT')
           }
        }
    }
    dependencies { // Not Published to Gradle Plugin Portal
        classpath "cloud.wondrify:asset-pipeline-gradle"
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-data-hibernate5"
        classpath "org.apache.grails:grails-gradle-plugins"
    }
}

plugins {
    id "war"
    id "idea"
    id "eclipse"
}

// Not Published to Gradle Plugin Portal
apply plugin: "org.apache.grails.gradle.grails-web"
apply plugin: "org.apache.grails.gradle.grails-gsp"
apply plugin: "cloud.wondrify.asset-pipeline"

group = "com.example"

repositories {
    mavenCentral()
    maven {
       url = 'https://repo.grails.org/grails/restricted'
    }
    maven {
       url = 'https://repository.apache.org/content/groups/snapshots'
       content {
          includeVersionByRegex('org[.]apache[.](grails|groovy).*', '.*', '.*-SNAPSHOT')
       }
    }
}

configurations {
    hotswapAgent {
        canBeConsumed = false
        canBeResolved = true
        transitive = false
    }
}

dependencies {
    profile "org.apache.grails.profiles:web"
    testAndDevelopmentOnly "org.webjars.npm:bootstrap"
    testAndDevelopmentOnly "org.webjars.npm:bootstrap-icons"
    testAndDevelopmentOnly "org.webjars.npm:jquery"
    implementation platform("org.apache.grails:grails-bom:$grailsVersion")
    implementation "org.apache.grails:grails-core"
    implementation "org.apache.grails:grails-data-hibernate5"
    implementation "org.apache.grails:grails-databinding"
    implementation "org.apache.grails:grails-events"
    implementation "org.apache.grails:grails-gsp"
    implementation "org.apache.grails:grails-interceptors"
    implementation "org.apache.grails:grails-layout"
    implementation "org.apache.grails:grails-logging"
    implementation "org.apache.grails:grails-rest-transforms"
    implementation "org.apache.grails:grails-scaffolding"
    implementation "org.apache.grails:grails-services"
    implementation "org.apache.grails:grails-url-mappings"
    implementation "org.apache.grails:grails-web-boot"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    console "org.apache.grails:grails-console"
    runtimeOnly "cloud.wondrify:asset-pipeline-grails"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "com.zaxxer:HikariCP"
    runtimeOnly "org.fusesource.jansi:jansi"
    integrationTestImplementation testFixtures("org.apache.grails:grails-geb")
    testImplementation "org.apache.grails:grails-testing-support-datamapping"
    testImplementation "org.apache.grails:grails-testing-support-web"
    testImplementation "org.spockframework:spock-core"
    hotswapAgent "org.hotswapagent:hotswap-agent:2.0.2"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.JETBRAINS
    }
}

def downloadHotswapAgentTask = project.tasks.register('downloadHotswapAgent')
downloadHotswapAgentTask.configure {
    description = 'Resolves HotswapAgent JAR from Maven Central and places it in the toolchain JDK\'s lib/hotswap directory'
    group = 'build'

    def targetDir = new File(project.services.get(JavaToolchainService)
            .launcherFor(java.toolchain).get().metadata.installationPath.asFile,
            'lib/hotswap')

    def targetFile = new File(targetDir, 'hotswap-agent.jar')

    inputs.file configurations.hotswapAgent.singleFile
    outputs.file targetFile

    doLast {
        targetDir.mkdirs()

        def agentJar = configurations.hotswapAgent.singleFile
        if (!agentJar.exists()) {
            throw new GradleException("HotswapAgent JAR not found: ${agentJar.absolutePath}")
        }
        logger.lifecycle "Resolving HotswapAgent ${agentJar.name} from Maven Central and copying to $targetFile"
        copy {
            from agentJar
            into targetDir
            rename agentJar.name, 'hotswap-agent.jar'
        }
    }
}

def grAgentTargetDir = new File(project.layout.buildDirectory.get().asFile, 'gragent')
def grAgentTargetFile = new File(grAgentTargetDir, 'gragent.jar')

def downloadGrAgentTask = project.tasks.register('downloadGrAgent')
downloadGrAgentTask.configure {
    description = 'Downloads the Gragent JAR from GitHub and places it in the build directory'
    group = 'build'

    outputs.file grAgentTargetFile

    doLast {
        if (!grAgentTargetDir.exists()) {
            grAgentTargetDir.mkdirs()
        }
        logger.lifecycle "Downloading Gragent JAR to ${grAgentTargetFile.absolutePath}"
        new URL('https://github.com/JetBrains/intellij-community/raw/refs/heads/master/plugins/groovy/hotswap/gragent.jar').withInputStream { input ->
            grAgentTargetFile.withOutputStream { output ->
                output << input
            }
        }
    }
}

bootRun.configure {
    logger.lifecycle "Running Grails application with " + System.getProperty('java.vendor') + " JDK " + System.getProperty('java.version') + ", HotswapAgent and Gragent for enhanced class redefinition"

    jvmArgs = ['-XX:+AllowEnhancedClassRedefinition', '-XX:HotswapAgent=fatjar', "-javaagent:${grAgentTargetFile}"]
    dependsOn downloadGrAgentTask, downloadHotswapAgentTask
}

tasks.withType(Test) {
    useJUnitPlatform()
}

assets {
    excludes = [
            'webjars/jquery/**',
            'webjars/bootstrap/**',
            'webjars/bootstrap-icons/**'
    ]
    includes = [
            'webjars/jquery/*/dist/jquery.js',
            'webjars/bootstrap/*/dist/js/bootstrap.bundle.js',
            'webjars/bootstrap/*/dist/css/bootstrap.css',
            'webjars/bootstrap-icons/*/font/bootstrap-icons.css',
            'webjars/bootstrap-icons/*/font/fonts/*',
    ]
}